// Compile groovy
apply plugin: 'groovy'

dependencies {
    compile gradleApi()
    compile localGroovy()
}

// Generate groovy doc

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
}

//--------------------------------------------------------------------------------------------------
// Scripts to upload plugin to Bintray/JCenter [https://bintray.com/]
// > ./gradlew bintrayUpload
//--------------------------------------------------------------------------------------------------
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.github.dcendents.android-maven'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'gradle-hostplugin'

def mavenDesc = 'Gradle plugin to build host plugin android apks.'
def baseUrl = 'https://github.com/lrhehe/AndroidHostPlugin'
def siteUrl = baseUrl
def gitUrl = "${baseUrl}.git"
def issueUrl = "${baseUrl}/issues"

def licenseIds = ['Apache-2.0']
def licenseNames = ['The Apache Software License, Version 2.0']
def licenseUrls = ['http://www.apache.org/licenses/LICENSE-2.0.txt']
def inception = '2016'

def username = 'lrhehe'

install {
    repositories {
        mavenInstaller {
            pom.project {
                // Description
                name archivesBaseName
                description mavenDesc
                url siteUrl

                // Archive
                groupId project.group
                artifactId archivesBaseName
                version project.version

                // License
                inceptionYear inception
                licenses {
                    licenseNames.eachWithIndex { ln, li ->
                        license {
                            name ln
                            url licenseUrls[li]
                        }
                    }
                }
                developers {
                    developer {
                        name username
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allGroovy
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    println groovydoc.destinationDir
//    assert false
    from groovydoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray upload
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    apiUrl = 'https://api.bintray.com'
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "com.lrhehe.tools.build:gradle-hostplugin"
        desc = mavenDesc
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        licenses = licenseIds
        labels = ['android', 'plugin', 'gradle', 'hostplugin']
        publish = true
        publicDownloadNumbers = true
    }
}